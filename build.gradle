import org.jose4j.jwk.JsonWebKey
import org.jose4j.jwk.OctJwkGenerator

buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }
    dependencies {
        classpath "org.bitbucket.b_c:jose4j:0.5.2"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${project.'springboot.version'}"
    }
}

subprojects {
    task allDependenciesInsight(type: DependencyInsightReportTask, description: "Produce insight information for all dependencies") << {}
    task allDependencies(type: DependencyReportTask, description: "Display a graph of all project dependencies") {}
}

task wrapper(type: Wrapper, description: "Update the Gradle wrapper") {
    gradleVersion = project."gradle.version"
}

task generateKey(description: "Generate keys for the CAS server") << {
    int keySize = project.hasProperty('size') ? size.toInteger() : 512
    println "generating key of size ${keySize} (to change, pass `-Psize=int` where `int` is the size)"
    def octetKey = OctJwkGenerator.generateJwk(keySize)
    def params = octetKey.toParams(JsonWebKey.OutputControlLevel.INCLUDE_SYMMETRIC)
    println "key: ${params.get('k')}"
}
